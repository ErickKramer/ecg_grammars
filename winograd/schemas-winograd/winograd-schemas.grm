package winograd
import core

// Value (he valued the violin)
schema ValueSchema
  subcase of TransitiveAction
  roles
    evaluator
    valuedThing: RD
  constraints
     protagonist <--> evaluator
     patient <--> valuedThing
     valuedThing.ontological-category <-- @valuableEntity

schema GratitudeSchema
  subcase of TransitiveAction, BridgeSchema
  roles
    actionary: @thank
    theme: RD // "he thanked John FOR the box"
  constraints
     bridgePatient <--> agent
     bridgeAgent <--> patient
     kind <-- @thanks

schema BridgeSchema
  roles
    kind: @bridgeKind
    bridgePatient
    bridgeAgent
    bridgeTheme

schema DiscourseResponse
  subcase of Communication, BridgeSchema
  roles
    actionary: @respond
  constraints
    bridgePatient <--> speaker
    bridgeAgent <--> listener
    bridgeTheme <--> media
    kind <-- @response

schema DiscourseRepeat
  subcase of Communication, BridgeSchema
  roles
    actionary: @repeat
  constraints
    bridgeAgent <--> speaker
    bridgePatient <--> listener
    bridgeTheme <--> media
    kind <-- @repetition

schema ChildBearing
  subcase of TransitiveAction
  roles
    mother: RD
    child: RD
  constraints
    mother <--> agent
    child <--> patient
    mother.ontological-category <-- @parent
    child.ontological-category <-- @baby
    mother.gender <-- @female

schema MusicSchema
  subcase of TransitiveAction
  roles
    musician: RD
    piece: RD
    method: RD
  constraints
    musician <--> agent
    piece <--> patient
    musician.ontological-category <-- @person
    method.ontological-category <-- @musicalInstrument

/* Causing Clogging */
schema CreateObstacleSchema
  subcase of CausalTransitiveAction
  roles
    portal: RD
  constraints
    portal <--> patient
    causalAgent.ontological-category <-- @removableEntity // <= @physicalEntity // Not sure about <= arrow, or should be new @obstacle?
    portal.ontological-category <-- @portal


/* Cleaning a portal (ex: drain) */
schema CleaningSchema
  subcase of TransitiveAction
  roles
    cleaner
    portal: RD
  constraints
     protagonist <--> cleaner
     patient <--> portal
     portal.ontological-category <-- @portal


/* Removing an obstacle (ex: hair) */
schema RemovingSchema
  subcase of TransitiveAction
  roles
    remover
    obstacle: RD
  constraints
     protagonist <--> remover
     patient <--> obstacle
     obstacle.ontological-category <-- @removableEntity

schema StrengthSchema
  subcase of TransitiveAction
  roles
    agent
    comparingObject: RD // not necessarily?
  constraints
    protagonist <--> agent
    patient <--> comparingObject
    agent.ontological-category <-- @strongPerson
    comparingObject.ontological-category <-- @weakEntity

schema RelativeScale
	subcase of Relation
	roles
		larger
		smaller
		property
	constraints
		larger <--> entity1
		smaller <--> entity2
		relation <--> property
		relation <-- @size

schema ContainSchema
  subcase of Motion
    evokes SPG as spg
    evokes RelativeScale as rsc
  roles
    container: RD
    containedObject: RD
  constraints
    container <--> rsc.larger
    containedObject <--> rsc.smaller
    containedObject <--> mover
    container <--> spg.landmark
    container.ontological-category <-- @container
    containedObject.ontological-category <-- @physicalEntity
 
schema RelativeMotion
	subcase of Motion, TransitiveAction
	evokes SPG as spg
	evokes RelativeScale as rsc
	roles
		object1: RD
		object2: RD
	constraints
		object1 <--> agent
		object2 <--> patient
		object1 <--> mover
		object1 <--> spg.trajector
		object2 <--> spg.landmark
		rsc.relation <-- @speed
		object1 <--> rsc.larger
		object2 <--> rsc.smaller
		
		