package hesperian
import base

general construction ConditionPhrase
  subcase of NP

// e.g. stomach pain
construction LocatedCondition
  subcase of NounNounCompound, ConditionPhrase
  meaning: Condition
    constraints
      self.m <--> head.m
      self.m.location <--> mod.m
      head.m.ontological-category <-- @condition
      mod.m.ontological-category <-- @bodyPart2

// e.g. severe pain
construction ConditionSeverity
  subcase of ConditionPhrase
  constructional
    constituents
      symp: ConditionPhrase
      sev: ScalarAdjectiveType
  form
  	constraints
  	  sev.f meets symp.f
  meaning: Condition
    constraints
      self.m <--> symp.m
      sev.m <--> self.m.severity

// e.g. cancer pain
construction DiseaseSymptom
  subcase of ConditionPhrase
  constructional
    constituents
      symp: ConditionPhrase
      dis: ConditionPhrase
  meaning: Disease
    constraints
      dis.m <--> self.m
      symp.m <--> self.m.symptom
      symp.m.ontological-category <-- @symptom
      dis.m.ontological-category <-- @disease

// e.g. pain in stomach
construction Prep-Locative-Condition
  subcase of ConditionPhrase
  constructional
    constituents
      loc: HasNominalFeatures // This is probably way too loose but needed to accomodate kernels, e.g. "left abdomen"
      prep: LocativePreposition
      cond: ConditionPhrase
  form
    constraints
      cond.f before prep.f
      prep.f before loc.f
  meaning: Condition
    constraints
   	  self.m <--> cond.m
   	  self.m.location <--> loc.m

// DOES NOT WORK. FIX ME
/*construction Treatment-For-Condition
  constructional
    constituents
      trt: DrugType
      for: For-PP
  form
    constraints
      trt.f before for.f
  meaning: Treatment
    constraints
      self.m <--> trt.m
     // self.m.condition <--> for.np.m
     // for.np.m.ontological-category <-- @condition
*/

// e.g. stomach medicine
construction LocatedTreatment
  subcase of NounNounCompound, NP
  meaning: Treatment
    constraints
      self.m <--> head.m
      self.m.location <--> mod.m
      mod.m.ontological-category <-- @bodyPart2
      head.m.ontological-category <-- @treatment

// e.g. cancer medicine
construction ConditionTreatment
  subcase of NounNounCompound, NP
  meaning: Treatment
    constraints
      self.m <--> head.m
      self.m.condition <--> mod.m
      mod.m.ontological-category <-- @condition
      head.m.ontological-category <-- @treatment

// Copied from NounNounCompound
// e.g. abortion pills
construction ProcedureDrug
  subcase of Nominal, NP
  constructional
    constituents
      mod: Nominal
      head: Nominal
    constraints
      self.features <--> head.features
  form
    constraints
      mod.f before head.f
  meaning: Treatment
    evokes NounNounModifier as nnm
    constraints
      nnm.modifiedThing <--> head.m
      nnm.modifier <--> mod.m
      self.m <--> mod.m
      self.m.drug <--> head.m
      head.m.ontological-category <-- @drug
      mod.m.ontological-category <-- @procedure


// ALSO DOESN'T WORK. FIX ME
/*construction Drug-For-Person
  subcase of PP
  constructional
    constituents
      prep: For-Recipient
      person: SentientNoun
      np: DrugType
  form
    constraints
      prep.f before person.f
      np.f before prep.f
*/
construction After-Event
  subcase of Preposition
  form
    constraints
      self.f.orth <-- "after"

construction After-PP
  subcase of PP
  constructional
    constituents
      prep: After-Event
  form
   constraints
     prep.f before np.f
  meaning
    constraints
      self.m <--> np.m

// e.g. bleeding for four weeks
construction Condition-For-Time
  subcase of ConditionPhrase
  constructional
    constituents
      symp: ConditionPhrase
      optional prep: For-Recipient
      np: TemporalQuantityNP
  form
    constraints
      symp.f before prep.f
      prep.f before np.f
  meaning: Condition
    constraints
      self.m <--> symp.m
      self.m.duration <--> np.m

// e.g. bleeding after abortion
construction ConditionAfterTreatment
  subcase of ConditionPhrase
  constructional
    constituents
      cond: ConditionPhrase
      trigger: After-PP
  meaning: Condition
    constraints
      self.m <--> cond.m
      self.m.trigger <--> trigger.m

construction HesperianBag
  subcase of RootType
  constructional
    constituents
      np: NP
      optional cc: CoordinatingConjunction
      optional hb: HesperianBag
  form
    constraints
      np.f before cc.f
      cc.f before hb.f
  meaning: HesperianBagSchema
    constraints
      self.m.this <--> np.m
      self.m.next <--> hb.m
      self.m.this.ontological-category <-- @hesperian
