package hesperian
import base

// e.g. cancer pain
construction DiseaseSymptom
  subcase of NP
  constructional
    constituents
      symp: NP
      dis: NP
  meaning: Disease
    constraints
      dis.m <--> self.m
      symp.m <--> self.m.symptom
      symp.m.ontological-category <-- @symptomType
      dis.m.ontological-category <-- @diseaseType

// e.g. pain in stomach
construction Prep-Locative-Condition
  subcase of NP
  constructional
    constituents
      loc: NP
      prep: LocativePreposition
      cond: NP
  form
    constraints
      cond.f before prep.f
      prep.f before loc.f
  meaning: Condition
    constraints
   	  self.m <--> cond.m
   	  self.m.location <--> loc.m
   	  self.m.ontological-category <-- @conditionType

// e.g. medicine for cough
construction Treatment-For-Condition
  subcase of NP
  constructional
    constituents
      trmt: NP
      for: For-PP
  form
    constraints
      trmt.f before for.f
  meaning: Treatment
    constraints
      self.m <--> trmt.m
      trmt.m.ontological-category <-- @treatmentType
      self.m.condition <--> for.np.m
      for.np.m.ontological-category <-- @conditionType

// e.g. ampicillin for men
construction Treatment-For-Person
  subcase of NP
  constructional
    constituents
      trmt: NP
      for: For-PP
  form
    constraints
      trmt.f before for.f
  meaning: Treatment
    constraints
      self.m <--> trmt.m
      trmt.m.ontological-category <-- @treatmentType
      self.m.patient <--> for.np.m
      for.np.m.ontological-category <-- @patientType

construction After-Event
  subcase of Preposition
  form
    constraints
      self.f.orth <-- "after"

construction After-PP
  subcase of PP
  constructional
    constituents
      prep: After-Event
  form
   constraints
     prep.f before np.f
  meaning: RD
    constraints
      self.m <--> np.m

// e.g. bleeding for four weeks
construction Condition-For-Time
  subcase of NP
  constructional
    constituents
      symp: NP
      optional prep: For-Recipient // This should be For-Temporal
      np: TemporalQuantityNP
  form
    constraints
      symp.f before prep.f
      prep.f before np.f
  meaning: Condition
    constraints
      self.m <--> symp.m
      self.m.duration <--> np.m
      symp.m.ontological-category <-- @conditionType

// e.g. bleeding after abortion
construction ConditionAfterTreatment
  subcase of NP
  constructional
    constituents
      cond: NP
      trigger: After-PP
  meaning: Condition
    constraints
      self.m <--> cond.m
      self.m.trigger <--> trigger.m
      cond.m.ontological-category <-- @conditionType

construction HesperianBag
  subcase of RootType
  constructional
    constituents
      np: NP
      optional cc: CoordinatingConjunction
      optional hb: HesperianBag
  form
    constraints
      np.f before cc.f
      cc.f before hb.f
  meaning: HesperianBagSchema
    constraints
      self.m.this <--> np.m
      self.m.next <--> hb.m
      self.m.this.ontological-category <-- @hesperian

// e.g. severe pain
// Note: Undetermined_KernelNP should take care of this but for some reason does not
construction Undetermined_Kernel
  subcase of NP
  constructional
    constituents
      k: Kernel
    constraints
      self.features <--> k.features
  meaning
    constraints
      self.m <--> k.m

// e.g. injection in vein
construction Prep-Locative-Treatment
  subcase of NP
  constructional
    constituents
      loc: NP
      prep: LocativePreposition
      trmt: NP
  form
    constraints
      trmt.f before prep.f
      prep.f before loc.f
  meaning: Treatment
    constraints
      self.m <--> trmt.m
      self.m.location <--> loc.m
      trmt.m.ontological-category <-- @treatmentType

// e.g. sore breast, stiff leg
construction DescribedBodyPart
  subcase of NP
  constructional
    constituents
      adj: ScalarAdjective
      bp: NP
  form
    constraints
      adj.f before bp.f
  meaning: Symptom
    constraints
      bp.m.ontological-category <-- @bodyPart2
      self.m.location <--> bp.m
      self.m.ontological-category <--> adj.m.property
      self.m.severity.value <--> adj.m.value

