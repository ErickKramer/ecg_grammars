package hesperian
import base

// e.g. cancer pain
construction DiseaseSymptom
  subcase of NP
  constructional
    constituents
      symp: NP
      dis: NP
  meaning: Disease
    constraints
      dis.m <--> self.m
      symp.m <--> self.m.symptom
      symp.m.ontological-category <-- @symptomType
      dis.m.ontological-category <-- @diseaseType

construction HesperianBag
  subcase of RootType
  constructional
    constituents
      np: NP
      optional cc: CoordinatingConjunction
      optional hb: HesperianBag
  form
    constraints
      np.f before cc.f
      cc.f before hb.f
  meaning: HesperianBagSchema
    constraints
      self.m.this <--> np.m
      self.m.next <--> hb.m
      self.m.this.ontological-category <-- @hesperian

// e.g. severe pain
// Note: Undetermined_KernelNP should take care of this but for some reason does not
construction Undetermined_Kernel
  subcase of NP
  constructional
    constituents
      k: Kernel
    constraints
      self.features <--> k.features
  meaning
    constraints
      self.m <--> k.m

// e.g. sore breast, stiff leg
construction DescribedBodyPart
  subcase of NP
  constructional
    constituents
      adj: ScalarAdjective
      bp: NP
  form
    constraints
      adj.f before bp.f
  meaning: Symptom
    constraints
      bp.m.ontological-category <-- @bodyPart2
      self.m.location <--> bp.m
      self.m.ontological-category <--> adj.m.property
      self.m.severity.value <--> adj.m.value

// Note: Pretty hacky but works for this purpose.
construction Again
  subcase of Adverb
  form
    constraints
      self.f.orth <-- "again"
  meaning: AdverbModification
    constraints
      self.m.modifier <-- "again"

// Copied from core adverb-extended package
// e.g. run quickly
construction ModifiedVerb
  subcase of Verb
  constructional
    constituents
      modifier: Adverbial
      modifiedVerb: Verb
    constraints
      self.features <--> modifiedVerb.features
  meaning
    constraints
      modifier.m.modifiedThing <--> modifiedVerb.m
      self.m <--> modifiedVerb.m
