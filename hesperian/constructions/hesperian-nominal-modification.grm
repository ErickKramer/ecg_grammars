package hesperian
import base

// e.g. stomach pain
construction LocatedCondition
  subcase of NounNounCompound, NP
  meaning: Condition
    constraints
      self.m <--> head.m
      self.m.location <--> mod.m
      head.m.ontological-category <-- @conditionType
      mod.m.ontological-category <-- @bodyPart2

// e.g. stomach medicine
construction LocatedTreatment
  subcase of NounNounCompound, NP
  meaning: Treatment
    constraints
      self.m <--> head.m
      self.m.location <--> mod.m
      mod.m.ontological-category <-- @bodyPart2
      head.m.ontological-category <-- @treatmentType

// e.g. cancer medicine
construction ConditionTreatment
  subcase of NounNounCompound, NP
  meaning: Treatment
    constraints
      self.m <--> head.m
      self.m.condition <--> mod.m
      mod.m.ontological-category <-- @conditionType
      head.m.ontological-category <-- @treatmentType

// e.g. two year old
construction AgedPerson
  subcase of Nominal, NP
  constructional
    constituents
      mod: NumberType
      head: Nominal
  form
    constraints
      mod.f before head.f
  meaning: Patient
    constraints
      self.m <--> head.m
      self.m.numericalAge <--> mod.m
      head.m.ontological-category <-- @patientType

// Copied from NounNounCompound
// e.g. abortion pills
construction ProcedureDrug
  subcase of Nominal, NP
  constructional
    constituents
      mod: Nominal
      head: Nominal
    constraints
      self.features <--> head.features
  form
    constraints
      head.f before mod.f
  meaning: Procedure
    evokes NounNounModifier as nnm
    constraints
      nnm.modifiedThing <--> head.m
      nnm.modifier <--> mod.m
      self.m <--> head.m
      self.m.drug <--> mod.m
      mod.m.ontological-category <-- @drugType
      head.m.ontological-category <-- @procedureType
