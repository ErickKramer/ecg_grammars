/* 
This is for testing cxns like:
"able to X"
"unable to X"

It could be analyzed as a property adjective, but it's clearly not the same
as other property adjectives like "red" or "big". It describes a state, but it's 
a state as it relates to some action/event.

it also always seems to follow a copula
*/

package purchases
//import control

schema Capability
    subcase of Process
	roles
	  capable_event: EventDescriptor
	  //event: EventDescriptor // event that entity can participate in
	  entity: RD // entity who can either meet or fall short of conditions
	  // non-core elements
	constraints
	  protagonist <--> entity

construction AbleAdjective
	subcase of PropertyAdjectiveType
	meaning
	 evokes Capability as capability
	 evokes EventFeatures as ef
	 constraints
	   ef.modality <-- @can
	   ef.negated <-- @boolean
	   self.m.modifier <--> capability
	   self.m.property <-- @capability
	   self.m.value <-- @able-value
	   
import auxiliaries
	   
construction AbleTo
	subcase of VP
 	constructional
 	 constituents
 	   be: BeAux
 	   able: AbleAdjective
 	   vp: VP
 	  constraints
 	   vp.verbform <-- Infinitive
 	form
 	 constraints
 	   be.f before able.f
 	   able.f before vp.f
   meaning: Capability
     constraints
       self.ed.profiledParticipant <--> vp.ed.profiledParticipant
       self.m <--> able.capability
       self.m.actionary <--> able.m.value
       vp.ed.e-features <--> able.ef
       self.ed.e-features <--> able.ef
       able.m.modifiedThing <--> vp.ed.profiledParticipant // a bit hacky, but this propagates protagonist bindings
       self.m.capable_event <--> vp.ed
       self.m.entity <--> vp.ed.profiledParticipant
       
	   