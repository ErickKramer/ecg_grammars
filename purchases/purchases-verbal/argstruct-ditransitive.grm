package purchases
import purchases-testing


/* 
These are just some starter constructions to test some analyses of the purchasing frame.

Ultimately, we will want finer-grained constructions. As described in schemas-purchase.grm,
we want a construction that doesn't bind "buyer" to "getter" ("he bought her a pizza"). 

*/

//this is a general construction used for inheritance
general construction ActiveDitransitive // he Xed Indirect Direct  8/16/13
  subcase of ArgumentStructure
  constructional
    constituents
      np1: NP
      np2: NP  
    constraints
      v.argform <-- DiTransitive  //2/11/13
      v.voice <-- NotPassive  // 1/14/14
      np1.features.case <-- "dat"
      np2.features.case <-- "acc"
  form
    constraints
       v.f before np1.f
       np1.f before np2.f
  meaning:Process //  8/16/13
     evokes A123 as s
    constraints
       s.a1 <--> ed.profiledParticipant
       s.a2 <--> np1.m // recipient  // 8/22/13
       s.a3 <--> np2.m  // theme  
       

//this cxn  binds the meaning of "give" to the meaning of the cxn

general construction CentralActiveDitransitiveTransfer   // 8/24/13
  subcase of ActiveDitransitive
  meaning: ObjectTransfer  // 2/11/13
    constraints
      	self.m <--> v.m
  		self.m.donor <--> ed.profiledParticipant
       	self.m.recipient <--> np1.m
       	self.m.theme <--> np2.m


// "he sold the man a pizza"
// "
construction GoodsTransferDiTransitiveCxn
  subcase of ActiveDitransitive
  constructional
    constituents
      optional np1: NP
  meaning: GoodsTransfer  // 2/11/13
    constraints
        np1.m.ontological-category <-- @sentient
      	self.m <--> v.m
      	self.m.recipient <--> np1.m
  		self.m.donor <--> ed.profiledParticipant
       	self.m.buyer <--> np1.m
       	self.m.theme <--> np2.m
       //	self.m.recipient <--> np1.m // maybe?
       	self.m.seller <--> self.m.ed.profiledParticipant

construction GoodsTransferTO-PP
  subcase of ActiveTransitive
  constructional
    constituents
      pp: TO-pp
  meaning: GoodsTransfer  // 2/11/13
    constraints
        //pp.np.m.ontological-category <-- @sentient
        // Not sentient: "he sold the goods back to the bank..."?
      	self.m <--> v.m
      	self.m.recipient <--> pp.np.m
  		self.m.donor <--> ed.profiledParticipant
       	self.m.theme <--> np.m
       	self.m.seller <--> self.m.ed.profiledParticipant
       	self.m.buyer <--> pp.np.m


// np1 is optional: "he bought a pizza". "he bought the man a pizza"
construction MoneyTransferCxn 
  subcase of ActiveDitransitive
  constructional
    constituents
      optional np1: NP
      optional source: FROM-pp
   form
    constraints
       np2.f before source.f
  meaning: MoneyTransfer  // 2/11/13
    constraints
        np1.m.ontological-category <-- @sentient
      	self.m <--> v.m
  		self.m.donor <--> ed.profiledParticipant
       	self.m.recipient <--> np1.m
       	self.m.theme <--> np2.m
       	source.np.m <--> self.m.seller
		self.m.buyer <--> self.m.ed.profiledParticipant

	