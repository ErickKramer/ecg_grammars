package premier

//
// sentence.grm
//
// The general cxn S is the root of most (all?) speech acts /

general construction S
    subcase of RootType 
    meaning: EventDescriptor


// This general cxn does the agreement between subj and verb
// 6/17/14 (ST): made NP optional, because Spanish doesn't require an explicit subject (might need to modify this later)
general construction S-With-Subj  
    subcase of S
    constructional
        constituents
            subj: NP
            fin: HasVerbFeatures  // for modals, not VP
        constraints
        	fin.verbform <-- Finite	
        	subj.features.person <--> fin.features.person
        	subj.features.number <--> fin.features.number
            subj.features.case <-- "nom"
    meaning
        constraints
	       subj.m <--> self.m.profiledParticipant

	       	
construction Declarative
    subcase of S-With-Subj
    constructional
        constituents
	       fin: VP
    form
        constraints
	       subj.f before fin.f
    meaning
        constraints
           self.m <--> fin.ed
		   fin.ed.profiledParticipant.ontological-category <--> subj.m.ontological-category  
		   

// Simple imperative, testing
general construction Imperative
  subcase of S

construction SimpleImperative
  subcase of Imperative
  constructional
    constituents
      vp: VP
    constraints
      vp.verbform <-- Imperative
  meaning
    constraints
      self.m <--> vp.ed  				   
		
//  Utterance, Discourse Element -- Questions
// Uncomment all this below to fully analyze "was the block red ? "


general construction Utterance
    subcase of RootType 
    constructional 
        constituents
            core: S
            optional end: EMark  // punctuation like . ! ?
    form 
        constraints  
            core.f meets end.f
    meaning: DiscourseElement
        constraints
          self.m.content <--> core.m
construction YNQuesUtterance  
    subcase of Utterance
    constructional 
        constituents
            core: YN-Question
            end: QMark    
    form
        constraints
            core.f meets end.f 
    meaning 
        constraints
            self.m.mood <-- "YN-Question" // this is where we want mood	
            self.m.content <--> core.m
            
general construction EMark  
    subcase of Word

construction IMark
   subcase of EMark
   form
      constraints
         self.f.orth <-- "!"
         
construction PMark
	subcase of EMark
	form
	  constraints
	    self.f.orth <-- "."         

construction Comma
	subcase of Word
	form
	   constraints
	      self.f.orth <-- ","

construction QMark
    subcase of EMark 
    form 
        constraints
            self.f.orth <-- "?" 

schema DiscourseElement //meaning of Utterance
    roles
        speaker: RD 	
        addressee: RD 	
        attentional_focus  
        content: EventDescriptor
        mood: @mood			// one of declarative, imperative, ...
    constraints
		speaker.ontological-category <-- @sentient
		addressee.ontological-category <-- @sentient

           


        