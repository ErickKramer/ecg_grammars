package conditionals
import conjunctions


schema ConditionalSchema
	roles
	  condition: EventDescriptor // maps onto condition
	  conditionalValue: @temporality // needs work, but this should differentiate between "if" and "while"
	  premise: EventDescriptor
	  conclusion: EventDescriptor
	  alternative: EventDescriptor
	  
	    
general construction ConditionalConjunction
  subcase of Word
  meaning: ConditionalSchema
   constraints 
     self.m.condition <--> self.m.premise

construction Then-Conditional
  subcase of Word
  form
    constraints
      self.f.orth <-- "then"

construction If
  subcase of ConditionalConjunction
  form
    constraints
      self.f.orth <-- "if"
  meaning
   constraints
     self.m.conditionalValue <-- @bounded

construction While
  subcase of ConditionalConjunction
  form
    constraints
      self.f.orth <-- "while"
  meaning
   constraints
     self.m.conditionalValue <-- @ongoing


// contains a conjunction ("if", "while") and a general declarative
// "if it rains", "while it is raining", "if he pushes the box"
construction ConditionalClause
	subcase of RootType
	constructional
	 constituents
	  cond: ConditionalConjunction
	  bool: Declarative
    form
     constraints
      cond.f meets bool.f
    meaning: ConditionalSchema
     constraints
       self.m <--> cond.m
       self.m.condition <--> bool.m
       

general construction ConditionalStatement
	subcase of S
	constructional
	 constituents
	   condition: ConditionalClause
	   core: S 
	   optional else: AlternativeClause
    meaning: ConditionalED
     constraints
       self.m <--> condition.m
       self.m.conclusion <--> core.m
       self.m.alternative <--> else.m.alternative

general construction ConditionalImperative
	subcase of ConditionalStatement, Imperative
	constructional
	  constituents
	    optional c: Comma
	    optional then: Then-Conditional
	    core: SimpleImperative
	    optional c2: Comma
	    optional else: AlternativeClause
	  constraints
	    core.vp.verbform <-- Base
	 form
	  constraints
	    core.f before c2.f
	    c2.f before else.f
	 meaning: ConditionalED
	  constraints
	    self.m.profiledParticipant <--> core.m.profiledParticipant
	    self.m.profiledParticipant <--> else.m.alternative.profiledParticipant
	    self.m.alternative <--> else.m.alternative

construction StandardConditionalImperative
	subcase of ConditionalImperative
	form
	  constraints
	    condition.f meets c.f
		c.f meets then.f
	    condition.f before core.f
	    core.f before else.f


construction InverseConditional	
	subcase of ConditionalImperative 
	form
	  constraints
	  	core.f meets condition.f

   
// Constructions for the "else-clause", e.g. "otherwise do X", or "otherwise X will happen" 
construction AlternativeClause
	subcase of RootType
	constructional
	  constituents
	    else: AlternativeConjunction
	    core: S
	meaning: ConditionalSchema
	  constraints
	    self.m.alternative <--> core.m

general construction AlternativeConjunction
	subcase of Conjunction

construction Else
	subcase of AlternativeConjunction
	form
	 constraints
	   self.f.orth <-- "else"

construction Otherwise
	subcase of AlternativeConjunction
	form
	 constraints
	   self.f.orth <-- "otherwise"	


// ConditionalDeclarative
// if he builds a house, it will be red
general construction ConditionalDeclarative
	subcase of ConditionalStatement, Declarative
	constructional
	  constituents
	    optional c: Comma
	    optional then: Then-Conditional
	    core: Declarative
	 meaning: ConditionalED
	  constraints
	    self.m.profiledParticipant <--> core.m.profiledParticipant


construction StandardConditionalDeclarative
	subcase of ConditionalDeclarative
	form
	  constraints
	    condition.f meets c.f
	    c.f meets then.f
	    then.f meets core.f
	    core.f meets else.f



/*
construction StandardConditionalDeclarative
	subcase of ConditionalDeclarative
	form
	  constraints
	    cond.f meets bool.f
	    bool.f meets then2.f
	    bool.f before core.f
	    core.f before else.f
	meaning
	  constraints
	   	self.m.ed1 <--> bool.m
	    self.m.ed2 <--> core.m	
	    //self.m.ed3 <--> else.m    
*/	          