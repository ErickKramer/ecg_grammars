package core
import adverbs
//import adjunct
//import serial
//
// vp.grm


general construction VP
 subcase of HasVerbFeatures //not currently a RootType *****************
  meaning: Process 
    evokes EventDescriptor as ed
    constraints
      self.m <--> ed.eventProcess
	  self.m.ed <--> ed // pointer from process to EventDescriptor


// subcases of ArgumentStructure are in individual argstruct.grm files
general construction ArgumentStructure 
  subcase of VP
  constructional
    constituents
      v : Verb 
    constraints
      self.features <--> v.features
      self.verbform <--> v.verbform
      self.voice <--> v.voice
   meaning
     evokes A123 as s  
     constraints
       v.m  <--> ed.profiledProcess
       self.m.p-features <--> v.m.p-features 
      

// The A123 schema serves as a way to refer to up to 3 argument roles within an Argument Structure cxn.  
//Additional semantic constraints on these roles arise via binding to other schema roles

schema A123 //  evoked by ArgumentStructure cxn
	roles
		a1:RD
		a2:RD
		a3		


//To add: VP-participle-VP: "he walked into the room pushing a box".

// Added from base 6/10/15 (ST)
// Generates multiple identical parses?
construction AdverbialVP
	subcase of VP
	constructional
	  constituents
	   modifier: Adverbial
	   modifiedVP: VP
	  constraints
	    self.features <--> modifiedVP.features
	meaning
	  constraints
	    modifier.m.modifiedThing <--> modifiedVP.m
	    self.m <--> modifiedVP.m
	    //ed.profiledProcess <--> mo
	    
// Staged VP	    

construction StagedVP
	subcase of VP
	  constructional
	    constituents
	      v: Verb
	      vp: VP
	    constraints
	      v.verbkind <-- Stage
	      v.expectedVerbForm <--> vp.verbform
	      self.features <--> v.features
	      self.verbform <--> v.verbform
	form
	  constraints
	    v.f meets vp.f
	meaning: StagedProcess
	  constraints
	    self.m.protagonist <--> ed.profiledParticipant
	    self.m.stageRole <--> v.m.actionary
	    ed.profiledProcess <--> vp.m
	    ed.profiledParticipant <--> vp.ed.profiledParticipant
	    self.m.process <--> vp.m
	    self.m <--> v.m


// Infinitives

construction InfinitiveCxn
	subcase of VerbChunk
	constructional
	  constituents
	    first: TO-Infinitive-Marker
	    second: VerbChunk
	  constraints
	    second.verbform <-- Infinitive
	    self.verbform <-- Infinitive
	    self.argform <--> second.argform
	    self.verbkind <--> second.verbkind
	    self.voice <--> second.voice
	form
	  constraints
	    first.f meets second.f
    meaning
      constraints
        self.m <--> second.m


// General cxn for adjunct vp 
general construction AdjunctVP   // general for other coda, purpose, etc.
 subcase of VP
  constructional
    constituents
      vp: VP
      pp: PP // changed from AdjunctPP
   constraints        
  	  self.features <--> vp.features
 	meaning      
      evokes AdjunctModification as adjunct     
  	constraints
		self.m <--> vp.m   
      	ed <--> vp.ed
		adjunct.modifiedThing <--> self.m
    	adjunct.modifier <--> pp.m

/*
// he walked/ran for 2 hours
construction TemporalVP   // walked for/in 2 minutes, needs work
  subcase of AdjunctVP
  constructional
    constituents
      pp: TemporalPhrase
  form
    constraints
      vp.f before pp.f  // could eliminate order
  meaning  
    constraints
      self.m <--> vp.m
      ed <--> vp.ed
      ed.e-features.duration <--> pp.m.ti // TemporalInterval
      ed.e-features.telicity <--> pp.m.telicity // @bounded,
      //adj.kind <-- @temporal gone
      */