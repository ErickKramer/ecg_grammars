package core


/*
5/24/13 copy of cxn in vp.grm:

general construction ArgumentStructure 
  subcase of VP
  constructional
    constituents
      v : Verb 
    constraints
      self.features <--> v.features
      self.verbform <--> v.verbform
      self.voice <--> v.voice
   meaning
     evokes A123 as s  
     constraints
       v.m  <--> ed.profiledProcess
       self.m.p-features <--> v.m.p-features 
*/

// Neutral transitive package
// Defines general semantics for different transitive constructions
// Then, CoreActiveTransitive and CorePassiveTransitive can extend these.
// Still in testing stage (1/8/16, ST).

general construction TransitivePerception  // 8/21/13 he saw the block, the block was seen by him
	subcase of ArgumentStructure
	meaning: Perception
	 constraints
		v.verbkind <-- Perceptual
		s.a1 <--> self.m.thinker	
		s.a2 <--> self.m.content


/** Added from dropbox "robots" by Sean Trott, 2/4/15. */
general construction TransitiveCauseEffect
  subcase of ArgumentStructure
	meaning: CauseEffect
      constraints
		s.a1 <--> self.m.causalAgent
		s.a2 <--> self.m.affectedEntity		


general construction TransitiveCauseEffect_central  // he pushed the block
  subcase of TransitiveCauseEffect
	meaning
      constraints
		v.m <--> self.m 
  		
// "he painted the house blue", "he made the house bigger" (actives)
// "the house was painted blue by him" (passive)
// This is somewhat too specific? (1/6/16, ST)
general construction TransitiveChangeProperty
	subcase of TransitiveCauseEffect
	constructional
	  constituents
	    adj: Adjective
	    //np: NP
	form
	  constraints
	  // v.f before adj.f
	meaning: CauseEffect //: Creation
	   evokes Change as change
	   evokes Creation as creation
	   constraints
	     self.m.causalProcess <--> v.m
	     v.m <--> creation
	     self.m.affectedProcess <--> change
	     change.state <--> adj.m
	     self.m.affectedEntity <--> change.state.modifiedThing
	     creation.createdThing <--> self.m.affectedEntity



general construction Trans_CauseChangeLocation 
  subcase of TransitiveCauseEffect
  constructional
   constituents
      optional dis: SpatialQuantityNP // 1/6/16 (ST) --> still necessary?
      optional dir: Directional 
      optional pp: Path-PP 
   constraints
	  form
     constraints
	   v.f before dis.f 
       dis.f before dir.f
       dir.f before pp.f   
   meaning // CauseEffect  // inherited 
	evokes  MotionPath as mp 
      constraints 
       	self.m.affectedProcess <--> mp
       	self.m.affectedEntity <--> mp.mover 
    	self.m.affectedEntity <--> mp.spg.trajector
    	mp.spg <--> pp.m 
   		mp.spg.distance <--> dis.m 
	    mp.heading <--> dir.m.tag 
	    self.m.affectedEntity.ontological-category <-- @physicalEntity 
	   // the above line was commented because it constrains @antecedent
	   // this is solved by having pronouns have no ont-cat (see pronouns)

general construction Trans_CauseChangeLocation_causeMotion
	subcase of Trans_CauseChangeLocation
	meaning 
	 constraints
	   self.m <--> v.m	    
	    
// "he created the box"
// "the box was created by John"
general construction TransitiveCreation
	subcase of ArgumentStructure
	meaning: Creation
	  constraints
	    //self.m <--> v.m
	    s.a1 <--> self.m.protagonist
	    s.a2 <--> self.m.createdThing	
	    
// "John entered the room"
// "the room was entered by John	    
general construction TransitiveChangeLocation 
	subcase of ArgumentStructure
	meaning: ChangeLocation
	  constraints
		s.a1 <--> self.m.mover	
		s.a2 <--> self.m.landmark
		