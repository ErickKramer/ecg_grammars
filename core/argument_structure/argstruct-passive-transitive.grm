// Package for defining passive variants of Transitive cxns.
//
// (5/5/16, ST)
//
// Includes:
// -the box was seen by the man
// -the box was painted (by the man)
// -the box was moved (by the man)
// -the box was pushed into the room (by the man)
// -the house was built (by the man)
// -the box was picked up (by the man)
// -the box was given to him (by the man)

package argstruct-passive-transitive
import base
import auxiliaries

general construction PassiveTransitive
  subcase of ArgumentStructure
  constructional
    constituents
      v:Verb // inherited
      optional byPhrase: ByAgentPhrase
    constraints
      v.argform <-- Transitive
      v.voice <-- Passive
  form
    constraints
      v.f before byPhrase.f
  meaning: Process
    evokes A123 as s
    constraints
      s.a1 <--> byPhrase.m.landmark
      s.a2 <--> ed.profiledParticipant
      v.m.p-features.voice <-- @passive

// This binds the self.m with the verb's meaning.
general construction CorePassiveTransitive
  subcase of PassiveTransitive
  meaning
    constraints
      self.m <--> v.m

// e.g. the block was seen by him
construction PassiveTransitivePerception
  subcase of CorePassiveTransitive, TransitivePerception

// e.g. "the room was entered by him"
construction PassiveTransitiveChangeLocation
  subcase of CorePassiveTransitive, TransitiveChangeLocation

// e.g. "the box was moved into the room by John"
construction PassiveTrans_CauseChangeLocation_mp
  subcase of PassiveTransitive, Trans_CauseChangeLocation_mp

// e.g. "the box was pushed into the room"
construction PassiveTrans_CauseChangeLoc_causalAction
  subcase of PassiveTransitive, Trans_CauseChangeLoc_causalAction

// e.g. "the box is owned by the man"
construction PassiveTransitivePossession
  subcase of CorePassiveTransitive, TransitivePossession

// e.g. "the box was given to John by the man"
construction PassiveTransitiveObjectTransfer
  subcase of CorePassiveTransitive, TransitiveObjectTransfer
