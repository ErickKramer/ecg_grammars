/* 
Object control package. Currently in beta. I am trying to rework the explicit-causation package to be more generalizable,
such that it uses the existing catenatives package.


Created 7/5/16, ST. 

*/

package object-control
import catenatives


/***
CAUSATIVES
******************************
*
*/


// he Xed Y to Z
general construction CatenativeVP_Infinitive
	subcase of CatenativeVP
	constructional
	  constraints
	    vp.verbform <-- Infinitive
	    

	   
	   
// he caused X to Y	    
general construction Transitive_ExplicitCausation
	subcase of ArgumentStructure
	constructional
	  constraints
	    v.verbkind <-- ForceDynamic
	    //v.expectedVerbForm <--> vp.verbform
	meaning: CauseEffect
     constraints
	    s.a1 <--> self.m.causalAgent
        s.a2 <--> self.m.affectedEntity

 // he caused John to move        
general construction ObjectControl_AgentEffect
   subcase of Transitive_ExplicitCausation, CatenativeVP_Infinitive
   meaning
     constraints
       	//self.m.protagonist.ontological-category <-- @sentient // maybe from verb
		s.a2 <--> vp.ed.profiledParticipant
		vp.m  <--> self.m.affectedProcess
		self.m <--> v.m
		

// he caused the block to move
construction ActiveTrans_ObjectControl_AgentEffect_Causation 
  subcase of ActiveTransitive, ObjectControl_AgentEffect
    form
     constraints
	   np.f before vp.f  
	   


/***
PREVENTING
******************************
*
*/

// he Xed Y from Zing
general construction CatenativeVP_Prevention
	subcase of CatenativeVP
	constructional
	  constituents
       tag1: FROM-Marker 
       vp: VP
	  constraints
	    vp.verbform <-- PresentParticiple   
   form 
     constraints
	   tag1.f before vp.f	   


		
// he caused the block to move
construction ActiveTrans_ObjectControl_AgentEffect_Prevention
  subcase of ActiveTransitive, CatenativeVP_Prevention, ObjectControl_AgentEffect
    form
     constraints
	  np.f before vp.f  
	  

/***
Causal Action
******************************
*
*/	  
	  
// Agent causing an event: "he caused the crisis"
general construction Transitive_AgentEvent_Causation //  
  subcase of ArgumentStructure
   constructional  
     constraints
       v.verbkind <-- ForceDynamic //this should constrain this cxn to 'causal' verbs, e.g. cause, prevent
  meaning: CausalAction
     constraints
	    v.m <--> self.m
        s.a1 <--> self.m.causalAgent
        s.a2 <--> self.m.effect
           

construction ActiveTransitive_AgentEventCausation //he caused the crisis 
  subcase of ActiveTransitive, Transitive_AgentEvent_Causation  