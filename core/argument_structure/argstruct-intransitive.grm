package core

general construction IntransitiveArgumentStructure  
  subcase of ArgumentStructure
   constructional
  	constraints
   		v.argform <-- NotTransitive  // constraint excludes Transitve verbs, e.g. see
  meaning: Process  
    //evokes A123 as s // inherited from ArgumentStructure
    constraints
      s.a1 <--> ed.profiledParticipant // 
      s.a1 <--> self.m.protagonist // 5/8/13
      s.a1.ontological-category <--> ed.profiledParticipant.ontological-category
      v.m.p-features.voice <--  @notPassive 

/*	  
construction BareIntransitive  //  e.g. He moved
  subcase of IntransitiveArgumentStructure  
  meaning 
    constraints
      self.m <--> v.m
*/
//the binding "self.m <--> v.m" also serves to link self.m protagonist to the verb protagonist

// General cxn for copulas, others (adjective, locative, etc.) inherit from this.
general construction CopularVP
	subcase of VP
	constructional
	  constituents 
	    cop: Verb
	  constraints
	    self.features <--> cop.features
	    cop.argform <-- Copula
	meaning: Stasis
	  constraints
	     self.m <--> cop.m
	     self.m.protagonist <--> ed.profiledParticipant
	     


// Used to analyze, e.g. "the block is in the room" 
construction CopularVPLocative
  subcase of CopularVP
  constructional
    constituents
       	pred: Loc-PP
  	constraints
  		cop.argform <-- Copula_Loc 
  form
    constraints
      cop.f before pred.f  
  meaning: Stasis
    constraints
        ed.profiledParticipant <--> pred.m.trajector 
        self.m.p-features.negated <--> pred.m.negated
        self.m.state <--> pred.m 
        pred.m.temporality <-- @bounded
      
//Used to analyze, e.g., "the block is red"
construction CopularVPAdjective  
  subcase of CopularVP
  constructional  
    constituents
      	adj: Adjective
  	constraints
  		cop.argform <-- Copula_Adj 
  form
    constraints
      	cop.f meets adj.f
  meaning: Stasis 
    constraints
        self.m.state <--> adj.m
        self.m.p-features.negated <--> adj.m.negated
        self.adj.m.modifiedThing <--> ed.profiledParticipant  // might be enough for Adj-N agreement 10/27/12
 		self.adj.m.domain <--> ed.profiledParticipant.ontological-category // Adj-N agreement 10/27/12
		adj.m.temporality <-- @bounded

//the color is red, the color of the box is red, etc.
construction CopularVPPropertyAdjective
  subcase of CopularVP
  constructional  
    constituents
      	adj: PropertyAdjective
  	constraints
  		cop.argform <-- Copula_Adj 
	form
    constraints
      	cop.f meets adj.f
  meaning: Stasis 
    constraints
        self.m.state <--> adj.m
        self.adj.m.property <--> ed.profiledParticipant.ontological-category 
 
construction CopularVPNominal // 2/14/13
  subcase of CopularVP
  constructional  
    constituents
      	nominal: NP
  form
    constraints
      	cop.f meets nominal.f
  meaning: Stasis
    evokes RefIdentity as ri
   constraints
        self.verbform <--> cop.verbform
        self.m.state <--> ri
        ri.first <--> ed.profiledParticipant
        ri.second <--> nominal.m   
        ri.second.ontological-category <-- @entity
 
 // TO DO...this was changed earlier to be measurepredication
 construction CopularVPNominalModification // 2/14/13
  subcase of CopularVP
  constructional  
    constituents
      	nominal: UnitsNP
  form
    constraints
      	cop.f meets nominal.f
  meaning: Stasis   //MeasurePredication
   constraints
        self.m.state <--> nominal.m
        //self.m.measurement <--> nominal.m
        self.m.temporality <-- @bounded
        //self.m.dimension <--> nominal.m.extras.quantity.property

 /*
Nominal copulas (e.g. "it is a box") could be analyzed using a CopuluarVPNominal cxn
such as the one shown below, whcih is a subcase of the general CopularVP cxn above.
Note that the current first grammar does not contain the RefIdentity schema referred to by this cxn.


Sentences such as "he is moving" can potentially be analyzed in various ways.
One way is through the use of a CopularVPParticiple cxn such as the one shown below.
Note that while the current grammar has a general PresentParticiple cxn, it does not
have any specific lexical cxns for present participles.    
   */
 
construction CopularVPParticiple // 10/16/13
  subcase of VP
  constructional  
    constituents
      	cop: Verb
      	pp: Verb
      	//pp: PresentParticiple
  	constraints
  		self.features <--> cop.features
  		pp.verbform <-- PresentParticiple
  		cop.argform <-- Copula  // check this ********** 
	form
    constraints
      	cop.f meets pp.f
  meaning: Stasis
     evokes ParticipleModification as pm 
    constraints
    	self.m <--> cop.m
        self.m.protagonist <--> ed.profiledParticipant
        self.m.state <--> pm
        pm.modifier <--> pp.m
        pm.modifiedThing <--> ed.profiledParticipant  // might be enough for Adj-N agreement 10/27/12

 		
 // Constructions for VP-change: "the box changed from red to blue"
// "the box changed to blue"
// "the plant grew"
general construction VPChange
   subcase of IntransitiveArgumentStructure
   constructional
	  constituents
	    v: Verb
	  constraints
	    self.features <--> v.features
	meaning: Change
	  constraints
	    self.m <--> v.m
	    self.m.temporality <-- @bounded
	    self.m.protagonist <--> ed.profiledParticipant
   

construction VPChangeAdj
	subcase of VPChange
	constructional
	  constituents
	    adj: Adjective
	 form
	   constraints
	     v.f before adj.f
	meaning: Change
	  constraints
	    self.m.temporality <-- @bounded
	    ed.profiledParticipant <--> adj.m.modifiedThing
	    self.m.state <--> adj.m

// the box grew; the box grew to the ceiling 
// Needs work: how to tie in QuantityNP, Path-PP into Change schema?
construction IntransitiveChange  
  subcase of IntransitiveArgumentStructure
   constructional
   constituents
     optional np: QuantityNP
 	 optional pp: Path-PP
  form
    constraints
      v.f before np.f
  meaning: Change  
    constraints
      self.m <--> v.m
	 // self.m.state.scale <--> np.m.
	
	
//MotionPath argument structure: "he moved to the block", "he moved north"    
 construction ActiveMotionPath   // he moved to the block
  subcase of IntransitiveArgumentStructure 
  constructional
    constituents
       optional dis: SpatialQuantityNP  // for more general version
       optional dir: Directional 
       optional pp: Path-PP
  constraints
        v.voice <-- NotPassive  
  form 
    constraints
      v.f before dis.f 
      dis.f before dir.f
      dir.f before pp.f   
  meaning: MotionPath
    constraints 
      self.m <--> v.m
      self.m.spg.distance <--> dis.m 
	  self.m.heading <--> dir.m.tag
      self.m.spg <--> pp.m
      
      
