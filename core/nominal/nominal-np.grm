// This file contains  constructions for noun phrases, with determiners and without determiners,
// as well as modified Proper Nouns, e.g. "southern France".

package nominal
import base

general construction NP
  subcase of RootType, HasNominalFeatures
  meaning: RD

// Proper Nouns
general construction ProperNounNP
  subcase of NP, Word

// Stuff for argstruct-transitive
general construction UnitsNP
  subcase of NP
  constructional
    constituents
      num: Quantifier
      k: Noun
    constraints
      self.features <--> k.features
  form
    constraints
      num.f meets k.f
  meaning: RD
    constraints
      self.m <--> k.m
      num.m <--> self.m.extras.quantity.amount
      self.m.extras.quantity.units <-> k.m.ontological-category

// stands alone, no Specifier
construction SpatialQuantityNP
  subcase of UnitsNP
  meaning: RD
    constraints
      k.m.ontological-category <-- @spatial-units

// stands alone, no Specifier
construction WeightQuantityNP
  subcase of UnitsNP
  meaning: RD
    constraints
      k.m.ontological-category <-- @weight

// stands alone, no Specifier
construction PriceQuantityNP
  subcase of UnitsNP
  meaning: RD
    constraints
      k.m.ontological-category <-- @price

// stands alone, no Specifier
construction TemporalQuantityNP
  subcase of UnitsNP
  meaning: RD
    constraints
      k.m.ontological-category <-- @temporal-units

// various uses including:  white as snow
general construction UndeterminedNP
  subcase of NP
  meaning: RD
    constraints
      self.m.givenness <-- @typeIdentifiable // Michael
      self.m.bounding <-- @indeterminate


// these constraints should be tighter
// the purpose of this is really for things like "southern France"
construction ModifiedProperNoun
  subcase of NP
  constructional
    constituents
      adj: Adjective
      np: ProperNounNP
    constraints
      self.features <--> np.features
  form
    constraints
      adj.f meets np.f
  meaning: RD
    constraints
      adj.m.modifiedThing <--> np.m
      self.m <--> np.m
      np.m.ontological-category <-- @location


// e.g. 2 red boxes, etc.
construction QuantityNP
  subcase of SpecifierPlusKernel
  constructional
    constituents
      q: Quantifier
      k: Kernel
    constraints
      self.features <--> k.features
  form
    constraints
      q.f before k.f
  meaning: RD
    constraints
      self.m <--> k.m
      self.m.extras.quantity.amount <--> q.m
      self.m.givenness <-- @uniquelyIdentifiable
      self.m.extras.quantity.units <--> self.m.ontological-category


// e.g. 2 of the red boxes, etc.
construction QuantityOfNP
  subcase of QuantityNP
  constructional
    constituents
      o1: OF-Modifier
      th1: The
  form
    constraints
      q.f meets o1.f
      o1.f meets th1.f
      th1.f meets k.f
      q.f before k.f
