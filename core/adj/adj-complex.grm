// Complex adverb phrases, including:
// -Serial adverbs ("big red")
// -Comparative phrases ("bigger than the box")
// -Post-modified adjectives ("as big as the box")
//
// (ST, 5/11/16)

package adjective-extended
import base

general construction ComplexAdjective
  subcase of Adjective

// e.g. big red car
construction SerialAdjective
  subcase of ComplexAdjective
  constructional
    constituents
      first: Adjective
      second: Adjective
  form
    constraints
      first.f meets second.f
  meaning
    constraints
      self.m <--> first.m
      second.m.temporality <-- @atemporal
      self.m.modifiedThing <--> second.m.modifiedThing


// Uses comparative modifier, which is subcase of "scalar modifier".
// e.g. bigger than my car
construction ComparativeAdjectivePhrase
  subcase of ComplexAdjective
  constructional
    constituents
      comp: Adjective
      than: Than
      ground: NP
  form
    constraints
      comp.f meets than.f
      than.f meets ground.f
  meaning: ComparativeAdjModifier
    constraints
      self.m.base <--> ground.m
      self.m <--> comp.m
      self.m.kind <-- @comparative


// e.g. red as a block
construction PostModifiedAdjective
  subcase of ComplexAdjective
  constructional
  	constituents
      optional opt: AS-Marker
      first: Adjective
      link: AS-Marker
    	second: NP
  form
    constraints
  		first.f meets link.f
  		link.f meets second.f
  meaning: ComparativeAdjModifier
    constraints
      self.m <--> first.m
      self.m.base <--> second.m
      self.m.kind <-- @comparative
      self.m.direction <-- @equal
