/*
Adverbial cxns, taken mostly from base. 

(ST, 5/11/16)

*/

package core

general construction Adverbial  // can be a phrase as well
  meaning: AdverbModification
  

general construction Adverb
  subcase of Word, Adverbial
  
// should there be additional semantics for these?
// maybe a scale or multiplier?
general construction Intensifier
	subcase of Adverb
	meaning: IntensifierModification
	
	
construction Very // 
  subcase of Intensifier
  form
    constraints
       self.f.orth <-- "very"
  meaning
	constraints
		self.m.modifier <-- "very"
		self.m.kind <-- @unmarked

construction Too // 
  subcase of Intensifier
  form
    constraints
       self.f.orth <-- "too"
  meaning: AdverbModification
	constraints
		self.m.modifier <-- "too"
		self.m.kind <-- @unmarked


construction ScalarAdverbType
	subcase of Adverb, GeneralTypeCxn
	meaning: ScalarAdverbModifier
	  constraints
	     self.m.property <-- @scalarValue
	     self.m.value <-- "*"
	     self.m.kind <-- @unmarked


// We already have modifiedVP?	  
construction ModifiedVerb  // should not be copula, ?needed  11/21/12
  	subcase of Verb
   	constructional 
   		constituents
			modifier: Adverbial
			modifiedVerb: Verb
		constraints
			self.features <--> modifiedVerb.features
	//form
	//  constraints
	//     modifiedVerb.f meets modifier.f
	meaning
		constraints
			modifier.m.modifiedThing <--> modifiedVerb.m
			self.m <--> modifiedVerb.m

construction NegatedAdverb
	subcase of Adverb
	constructional 
	 constituents
	   first: Not-event
	   second: Adverb
	form
	  constraints
	    first.f meets second.f
	meaning
	  constraints
	    self.m <--> second.m
	    first.m.negated <--> self.m.negated
	    self.m.negated <-- @yes	
	    
	    
general construction ComplexAdverbial
  subcase of Adverbial
  
// Maybe tighten constraints to have "first" be an intensifier.  
construction SerialAdverb
   subcase of ComplexAdverbial 
   	constructional 
   		constituents
          	first: Intensifier
	    	second: Adverbial
	form
      constraints
       	first.f meets second.f
   meaning
   	constraints
		self.m <--> second.m
		first.m.modifiedThing <--> second.m  

// Intensified adjective
construction IntensifiedAjective
   subcase of Adjective 
   	constructional 
   		constituents
          	first: Intensifier
	    	second: Adjective
	form
      constraints
       	first.f meets second.f
   meaning
   	constraints
		self.m <--> second.m
		first.m.modifiedThing <--> second.m  


// quickly and carefully		
construction CoordinatedAdverb
   subcase of ComplexAdverbial 
   	constructional 
   		constituents
          	first: Adverbial
          	conj: LogicalConjunction
	    	second: Adverbial
	form
      constraints
       	first.f meets conj.f
       	conj.f meets second.f
   meaning
   	constraints
		self.m <--> second.m
		first.m.modifiedThing <--> second.m.modifiedThing
		
		
// comparative adverb
construction ComparativeAdverbPhrase  // Xed faster than John (Xed)
   subcase of Adverbial, RootType
   	constructional 
   		constituents
          	first: Adverb
	    	than: Than //serial Conj. now  12/9/13 ************
	    	base: NP
	form
      constraints
       	first.f meets than.f
       	than.f meets base.f
   meaning
   	constraints
   		self.m.kind <-- @comparative
		self.m <--> first.m
		